import org.ajoberstar.grgit.Grgit

buildscript {
   repositories {
       google()
       jcenter()
    }

    dependencies {
        // Add dependency for build script,
        // so we can access Git from our
        // build script.
        classpath 'org.ajoberstar:grgit:1.1.0'
    }
}
plugins {
    id 'com.gladed.androidgitversion' version '0.4.4'
}

apply plugin: 'java'
apply plugin:'application'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jacoco'


ext {
    // Open the Git repository in the current directory.
    git = Grgit.open(file('.'))

    // Get commit id of HEAD.
    revision = git.head().id
    // Alternative is using abbreviatedId of head() method.
    // revision = git.head().abbreviatedId
}

/*
 * Gets the version name from the latest Git tag
 */
def getVersionTag = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


mainClassName = 'Driver'
group 'com.mtecresults'
project.version = getVersionTag()+"-${git.head().abbreviatedId}"


sourceCompatibility=1.8
targetCompatibility=1.8

repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

findbugs {
    ignoreFailures = true
    //toolVersion = "2.0.1"
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/findbugs")
    effort = "max"
}

pmd {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/pmd")
    toolVersion = '5.1.3'
    ruleSets = [
            'java-android',
            'java-basic',
            'java-braces',
            'java-clone',
            'java-codesize',
            'java-controversial',
            'java-coupling',
            'java-design',
            'java-empty',
            'java-finalizers',
            'java-imports',
            'java-j2ee',
            'java-javabeans',
            'java-junit',
            'java-logging-jakarta-commons',
            'java-logging-java',
            'java-migrating',
            'java-naming',
            'java-optimizations',
            'java-strictexception',
            'java-strings',
            'java-sunsecure',
            'java-typeresolution',
            'java-unnecessary',
            'java-unusedcode'
    ]
}

test {
    systemProperty "java.net.preferIPv4Stack", "true"
}

dependencies {
    implementation 'com.google.guava:guava:19.0'
    implementation 'com.github.cmagnuson:mylaps-tcp-server:0.2.9'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.jmockit:jmockit:1.39'
}
